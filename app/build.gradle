plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.firebase.crashlytics'
    id 'com.google.gms.google-services'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "fr.delcey.pokedexino"
        minSdk 21
        targetSdk 31 // Won't use sdk 32 until the sources are here!
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        debug {
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
        }
        release {
            minifyEnabled true
            firebaseCrashlytics {
                mappingFileUploadEnabled true
            }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += ["-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi"]
    }

    signingConfigs {
        debug {
            storeFile file('../keystore_debug.jks')
            storePassword 'android'
            keyAlias 'debug'
            keyPassword 'android'
        }
    }
}

dependencies {
    implementation platform('com.google.firebase:firebase-bom:29.3.1')

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'

    // liveData {} lambda
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.4.1"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Task.await extension
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.6.1'

    // Google Auth
    implementation 'com.google.android.gms:play-services-auth:20.2.0'

    // Firebase auth
    implementation 'com.google.firebase:firebase-auth-ktx'

    // Firebase Firestore
    implementation 'com.google.firebase:firebase-firestore-ktx'

    // Firebase Analytics
    implementation 'com.google.firebase:firebase-analytics-ktx'

    // Firebase Crashlytics
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.13.0'

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.3"
    implementation "com.google.code.gson:gson:2.8.9"

    // Room
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    testImplementation 'junit:junit:4.13.2'
    testImplementation "com.google.truth:truth:1.1.3"
    testImplementation "io.mockk:mockk:1.12.3"
    testImplementation("androidx.arch.core:core-testing:2.1.0") {
        // Removes the Mockito dependency bundled with arch core (wtf android ??)
        exclude group: 'org.mockito', module: 'mockito-core'
    }
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.1"

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}